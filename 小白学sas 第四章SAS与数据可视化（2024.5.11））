/*-----------第四章 sas与数据可视化--------*/
/*------------4.1 用菜单绘制统计图-----------*/
libname ddw "C:\Users\ddwf\Desktop\学习\sas练习";

data ddw.test53;
	input gender$ age$ height weight fbg;
	cards;
女	≥50岁	164	81	7.7
女	≥50岁	161	69	9.3
男	≥50岁	168	70	10.7
男	≥50岁	171	61	10.4
男	≥50岁	174	70	11.0
男	<50岁	174	85	10.3
女	≥50岁	162	68	6.2
女	<50岁	162	67	9.5
男	≥50岁	175	73	9.0
男	≥50岁	175	78	7.2
女	≥50岁	156	60	9.8
女	<50岁	165	65	9.5
女	≥50岁	155	75	8.3
男	≥50岁	182	73	7.2
女	≥50岁	172	70	10.6
女	<50岁	155	70	8.2
女	≥50岁	158	68	9.4
男	≥50岁	174	80	10.6
女	<50岁	165	67	10.0
男	<50岁	170	70	7.3
男	<50岁	170	67	9.5
女	<50岁	160	60	9.8
女	≥50岁	162	65	7.2
男	≥50岁	167	81	7.8
女	<50岁	158	77	10.8
男	<50岁	170	61	10.7
女	≥50岁	160	60	6.2
女	≥50岁	162	54	8.5
男	<50岁	172	75	7.1
女	≥50岁	155	55	7.5
男	≥50岁	172	75	6.9
女	<50岁	159	63	6.6
女	≥50岁	156	59	7.4
男	<50岁	168	64	8.1
女	≥50岁	175	74	9.8
女	≥50岁	154	54	7.4
男	≥50岁	174	75	8.6
男	<50岁	172	81	8.0
男	<50岁	174	82	7.9
女	<50岁	159	60	5.4
女	≥50岁	165	61	8.6
女	≥50岁	164	61	8.1
男	<50岁	175	73	7.6
女	≥50岁	158	70	10.0
男	≥50岁	160	75	9.3
女	≥50岁	156	54	9.6
男	≥50岁	170	71	9.9
女	<50岁	164	64	10.0
女	<50岁	160	67	8.9
女	<50岁	162	77	8.5
;
run;

data ddw.test54; /*在sasuser中生成数据集pop*/
	input age$ pop_m pop_f mor_m mor_f death_m death_f;
	cards;
0-岁		3261	3146		3	2		3	1
5-岁		17818	13989		1	1		1	1
10-岁		62742	54272		2	3		2	1
15-岁		39025	35864		6	4		5	3
20-岁		19732	20772		6	3		4	2
25-岁		29976	28203		4	5		3	3
30-岁		36911	34530		12	4		7	2
35-岁		36194	34873		15	19		14	14
40-岁		31292	29423		21	16		13	14
45-岁		31730	31900		62	29		53	19
50-岁		22642	23973		74	36		73	32
55-岁		11480	12272		70	33		69	32
60-岁		12836	13454		96	34		79	32
65-岁		11401	11701		122	68		119	63
70-岁		8491	8614		181	71		173	71
75-岁		4505	5832		98	59		97	58
80-岁		2193	3316		53	29		52	25
85-岁		877	1754		21	15		19	14
;
run;

data ddw.test55; /*在sasuser中生成数据集price，注意格式与表4.3不同*/
	input year city$ price salary;
	cards;
1999	北京	5647	1171
2000	北京	4919	1363
2001	北京	5062	1596
2002	北京	4764	1821
2003	北京	4737	2109
2004	北京	5053	2473
2005	北京	6788	2849
2006	北京	8280	3343
2007	北京	11553	3876
2008	北京	12418	4694
2009	北京	13799	4845
2010	北京	17782	5474
2011	北京	16852	6320
2012	北京	17022	7109
1999	天津	2251	921
2000	天津	2328	1040
2001	天津	2375	1192
2002	天津	2487	1355
2003	天津	2518	1554
2004	天津	3115	1813
2005	天津	4055	2106
2006	天津	4774	2390
2007	天津	5811	2912
2008	天津	6015	3479
2009	天津	6886	3749
2010	天津	8230	4414
2011	天津	8745	4636
2012	天津	8218	5450
1999	上海	3422	1387
2000	上海	3565	1544
2001	上海	3866	1815
2002	上海	4134	1997
2003	上海	5118	2275
2004	上海	5855	2507
2005	上海	6842	2862
2006	上海	7196	3432
2007	上海	8361	4109
2008	上海	8195	4714
2009	上海	12840	5296
2010	上海	14464	5990
2011	上海	14603	6419
2012	上海	14061	6683
;
run;

/*--------4.2用proc sgplot绘图-----------*/
proc sgplot data=ddw.test53;
	histogram fbg;
	density fbg;
	inset "正态性检验"  "  P=0.16"/textattrs=(size=9) position=topright;
	xaxis valueattrs=(size=12) labelattrs=(size=12);
	yaxis valueattrs=(size=12) labelattrs=(size=12);
run;

proc sgplot data=ddw.test55;
	band upper=price lower=0 x=year/legendlabel="房价";
	band upper=salary lower=0 x=year/legendlabel="工资";
	keylegend/location=inside;
	xaxis type=discrete label="年份" valueattrs=(size=10) labelattrs=(size=10);
	yaxis label="房价和工资水平" values=(0 to 20000 by 2000) 
		valueattrs=(size=12) labelattrs=(size=12);
	where city="北京";
run;

proc sgpanel data=ddw.test55;
	panelby city/columns=1;
	band upper=price lower=0 x=year/legendlabel="房价";
	band upper=salary lower=0 x=year/legendlabel="工资";
	colaxis type=discrete label="年份" valueattrs=(size=10) labelattrs=(size=10);
	rowaxis label="房价和工资水平" values=(0 to 20000 by 4000) 
		valueattrs=(size=12) labelattrs=(size=12);
run;

proc sgplot data=ddw.test54;
	vline age/response=pop_m legendlabel="男性总人数" lineattrs=(color=green pattern=2 thickness=10);
	vline age/response=mor_m y2axis legendlabel="男性发病数" lineattrs=(color=orange pattern=34 thickness=10);
	xaxis discreteorder=data label="年龄段";
	yaxis values=(0 to 65000 by 5000) label="男性总人数";
	y2axis value=(0 to 200 by 20) label="男性发病数";
	keylegend /location=inside down=2 position=top;
run;

proc sgplot data=ddw.test55;
	vbar year/response=price dataskin=sheen legendlabel="房价";
	vbar year/response=salary dataskin=sheen legendlabel="工资";
	xaxis label="年份";
	yaxis values=(0 to 20000 by 2000) label="房价与工资";
	keylegend/ location=inside;
	where city="北京";
run;

proc sgplot data=ddw.test55;
	vbar year/response=price dataskin=matte legendlabel="房价";
	vbar year/response=salary dataskin=matte legendlabel="工资" barwidth=0.5;

	/*通过barwidth设置柱子的宽度，可以让柱状图嵌套显示*/
	xaxis label="年份";
	yaxis values=(0 to 20000 by 2000) label="房价与工资";
	keylegend/ location=inside;
	where city="北京";
run;

proc sgplot data=ddw.test55;
	vbar year/response=price dataskin=gloss legendlabel="房价" barwidth=0.5
		fillattrs=(color=green transparency=0.5);
	vbar year/response=salary dataskin=gloss legendlabel="工资" barwidth=0.5
		fillattrs=(color=red transparency=0.5) discreteoffset=0.2;

	/*fillattrs选项的transparency可以设置柱子的透明度为50%*/
	/*通过discreteoffset让柱子向右位移20%，可以让柱状图交错显示*/
	/*当两个柱子的barwidth都设为0.4，且discreteoffset也为0.4，则实现了两个柱子的并排显示*/
	xaxis label="年份";
	yaxis values=(0 to 20000 by 2000) label="房价与工资";
	keylegend/ location=inside;
	where city="北京";
run;

data ddw.test56;
	set ddw.test55;

	if city="北京";
	dp=dif(price);

	if dp=. then
		dp=price;

	if dp<0 then
		type="跌";
	else type="涨";
run;

proc sgplot data=ddw.test56;
	vbar year/response=dp dataskin=sheen group=type datalabel;

	/*datalabel指定在柱子的上方或下方显示值的大小*/
	keylegend/location=inside;
	xaxis label="年份";
	yaxis values=(-1000 to 6000 by 1000) label="房价涨幅";
run;

proc sgplot data=ddw.test56;
	waterfall category=year response=dp /datalabel dataskin=pressed colorgroup=type;

	/*瀑布图使用category定义横坐标，colorgroup定义type分类显示*/
	keylegend /location=inside;
	yaxis label="price change";
run;

proc sgplot data=ddw.test56;
	waterfall category=year response=dp /datalabel dataskin=pressed colorgroup=type;

	/*瀑布图使用category定义横坐标，colorgroup定义type分类显示*/
	keylegend /location=inside down=2 valueattrs=(size=10);

	/*down=2代表将图例按两行显示*/
	xaxis label="年份" labelattrs=(size=10) valueattrs=(size=10);
	yaxis label="price change" values=(0 to 20000 by 2000) labelattrs=(size=10)
		valueattrs=(size=10);
run;

proc sgplot data=ddw.test55;
	bubble x=year y=price size=salary/group=city dataskin=gloss;
	keylegend /location=inside;
	xaxis type=discrete;
run;

proc sgplot data=ddw.test55;
	bubble x=year y=price size=salary/group=city dataskin=sheen;
	series x=year y=price /group=city;

	/*通过添加线图可以反应趋势，增加一体性和美观度*/
	keylegend /location=inside;
	xaxis type=discrete;
	yaxis value=(0 to 20000 by 2000);
run;

data ddw.test57;
	input category$ upper_bmi lower_bmi wt;
	lower_ht=(wt/lower_bmi)**.5;

	/*根据bmi计算方法产生身高的上限和下限变量*/
	upper_ht=(wt/upper_bmi)**.5;
	cards;
偏瘦 1 18.5 40
偏瘦 1 18.5 60
偏瘦 1 18.5 80
偏瘦 1 18.5 100
正常 18.5 24.0 40
正常 18.5 24.0 60
正常 18.5 24.0 80
正常 18.5 24.0 100
偏胖 24.0 27.0 40
偏胖 24.0 27.0 60
偏胖 24.0 27.0 80
偏胖 24.0 27.0 100
肥胖 27.0 30.0 40
肥胖 27.0 30.0 60
肥胖 27.0 30.0 80
肥胖 27.0 30.0 100
重度肥胖 30.0 50.0 40
重度肥胖 30.0 50.0 60
重度肥胖 30.0 50.0 80
重度肥胖 30.0 50.0 100
;

proc sgplot data=ddw.test57;
	band x=wt upper=upper_ht lower=lower_ht /group=category;

	/*group=category指定带状图的变量按不同颜色显示*/
	xaxis values=(40 to 100 by 10) label="体重(kg)"; /*指定x轴的刻度值和标题*/
	yaxis values=(1.5 to 2 by 0.1) label="身高(m)"; /*指定y轴的刻度值和标题*/
	keylegend / position=right down=6; /*指定图例显示位置和行数*/
run;

data ddw.test58;
	set ddw.test53;
	height=height/100;
run;

data ddw.test59;
	merge ddw.test57 ddw.test58;
run;

proc print data=ddw.test59;
run;

proc sgplot data=ddw.test59;
	band x=wt upper=upper_ht lower=lower_ht /group=category;

	/*group=category指定带状图的变量按不同颜色显示*/
	scatter x=weight y=height /group=gender markerattrs=(symbol=circlefilled);
	xaxis values=(40 to 100 by 10) label="体重(kg)"; /*指定x轴的刻度值和标题*/
	yaxis values=(1.5 to 2 by 0.1) label="身高(m)"; /*指定y轴的刻度值和标题*/
	keylegend / position=right down=6; /*指定图例显示位置和行数*/
run;

proc sgplot data=ddw.test55;
	symbolimage name=price1 image="C:\Users\ddwf\Desktop\学习\sas练习\卡卡西.jpg";

	/*选中要插入的图片，name为该图片命名*/
	symbolimage name=salary1 image="C:\Users\ddwf\Desktop\学习\sas练习\水门.jpg";
	vline year/response=price markers markerattrs=(symbol=price1 size=35);
	vline year/response=salary markers markerattrs=(symbol=salary1 size=35);
	xaxis label="年份";
	yaxis label="房价与工作" values=(0 to 20000 by 2000);
	where city="北京";
run;

proc sgplot data=ddw.test53 noautolegend;
	/*noautolegend代表不显示图例*/
	symbolimage name=女 image="C:\Users\ddwf\Desktop\学习\sas练习\卡卡西.jpg";

	/*图片的名字应与变量的值相对应*/
	symbolimage name=男 image="C:\Users\ddwf\Desktop\学习\sas练习\水门.jpg";
	styleattrs datasymbols=(女 男);
	scatter x=height y=fbg/group=gender markerattrs=(size=30);
run;

data ddw.test60;
	input study: $100. or lcl ucl weight;
	x1=or/(10**(weight*0.01/2));
	x2=or*(10**(weight*0.01/2));

	/*x1和x2变量用于确定森林图中OR值的点大小，可以让不同大小的OR值显示为大小不同的点*/
	ci=cats(or,"(",lcl,"-",ucl,")");

	/*新产生ci变量，用cats函数将OR和95%置信区间合并起来*/
	ciname="95%CI";                   /*产生一个具有固定值的新变量ciname*/

	if lowcase(study)="overall" then
		study1=study;
	else study2=study;

	/*利用if语句将overall和其它研究分开，因为其它是单独结果，overall是综合结果*/
	datalines;
Modano(1967) 0.590 0.096 3.634 4
Modano2(1969) 0.429 0.070 2.620 8
Leighton(1972) 0.394 0.076 2.055 8
Borodan(1981) 0.464 0.201 1.074 14
Novak(1992) 0.490 0.088 2.737 8
Stawer(1998) 1.250 0.479 3.261 12
Adams(1999) 0.143 0.082 0.250 16
Soloway(2000) 0.718 0.237 2.179 12
Truark(2002) 0.129 0.027 0.605 10
Fayney(2005) 0.313 0.054 1.805 8
Overall 0.328 0.233 0.462 .
;
run;

proc sgplot noborder data=ddw.test60;
	/*noborder表示不显示图形边框*/
	scatter y=study1 x=or /markerattrs=(size=12 symbol=diamondfilled color=black);

	/*这一句是绘制overall的点，显示为黑色菱形，大小为12*/
	highlow y=study2 high=x2 low=x1/lineattrs=(thickness=10 color=black);

	/*这一句是绘制10项研究的点，显示为不同的宽度，厚度均为10，颜色为黑色*/
	highlow y=study high=ucl low=lcl/lineattrs=(thickness=2 color=black);

	/*这一句是绘制10项研究和overall的置信区间线段宽度，颜色为黑色*/
	refline 1 / axis=x;

	/*这一句是在x=1的位置绘制一条竖直的参考线*/
	scatter y=study x=ciname / markerchar=ci x2axis markercharattrs=(size=12);

	/*这一句是在图中显示变量ci的值，按第二个x轴的设置来显示*/
	xaxis type=log min=0.01 max=10 minor offsetmax=0.3 offsetmin=0 display=(nolabel) valueattrs=(size=12);

	/*这一句是设置x轴，将它设置为log尺度，因为OR值是指数计算的；offsetmax选项的作用是把x轴右侧空出30%的位置，用于显示变量ci*/
	x2axis offsetmin=0.85 display=none;

	/*这一句是设置第二个x轴，左边空出85%的位置，用来显示森林图*/
	yaxis display=(noticks nolabel) discreteorder=data valueattrs=(size=12) reverse;

	/*这一句是设置y轴，不显示坐标轴的刻度和标签*/
run;

data ddw.test61;
	set ddw.test53;
	bmi=weight/(height/100)**2;
	length bd$8.;

	if bmi<18.5 then
		bd="偏瘦";
	else if bmi<24 then
		bd="正常";
	else if bmi<27 then
		bd="偏胖";
	else if bmi<30 then
		bd="肥胖";
	else bd="重度肥胖";
run;

proc template;
	define statgraph pie;
		begingraph;
			layout region;
				piechart category=bd/group=gender start=90 dataskin=sheen stat=pct;
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test61 template=pie;
run;

proc means data=ddw.test61 nway;
	class gender bd;
	var bmi;
	output out=ddw.test62 n=count;

proc print data=ddw.test62;
run;

proc template;
	define statgraph mosaic;
		begingraph;
			layout region;
				mosaicplotparm category=(gender bd) count=count/
					insidevalueattrs=(size=12) labelattrs=(size=12) 
					valueattrs=(size=12) colorgroup=bd;
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test62 template=mosaic;
	label bd="体型" gender="性别";
run;

proc means data=ddw.test61 nway;
	class gender bd age;
	var bmi;
	output out=ddw.test63 n=count;

proc print data=ddw.test63;
run;

proc template;
	define statgraph mosaic;
		begingraph;
			layout region;
				mosaicplotparm category=(gender bd age) count=count/
					insidevalueattrs=(size=12) labelattrs=(size=12) 
					valueattrs=(size=12) colorgroup=bd;
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test63 template=mosaic;
	label bd="体型" gender="性别" age="年龄";
run;

data ddw.test64;
	input age$ pop_m;
	cards;
<50 308681
>=50 74425
;
run;

proc template;
	define statgraph brokenaxis;
		begingraph;
			layout overlay/xaxisopts=(tickvalueattrs=(size=12) label="年龄" 
				labelattrs=(size=12))  yaxisopts=(tickvalueattrs=(size=12) 
				linearopts=(includeranges=(0-100000 280000-320000))  
				label="男性人口" labelattrs=(size=12));
				barchart category=age response=pop_m / dataskin=gloss barwidth=0.5;

				/*barchart语句指定分类变量和分析变量，并设置宽度为0.5，色泽是gloss*/
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test64 template= brokenaxis;
run;

data ddw.test65;
	do i=1 to 5000;
		height=round(rand("normal",170,10),1);

		/*生成正态分布的变量，均值为170，标准差为10,并将变量四舍五入为整数*/
		weight=round(rand("normal",70,5),1);

		/*生成正态分布的变量，均值为70，标准差为5*/
		output;
	end;
run;

proc means data=ddw.test65 nway completetypes noprint;
	class height weight;
	var height;
	output out=ddw.test66(keep=height weight count) n=count;
run;

proc template;
	define statgraph bihistogram;
		begingraph;
			layout overlay3d / cube=false  zaxisopts=(griddisplay=on);
				bihistogram3dparm x=height y=weight z=count/display=all;
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test66 template=bihistogram;
run;

proc template;
	define statgraph heatmap;
		begingraph;
			layout overlay;
				heatmapparm x=height y=weight colorresponse=count/name="heatmapparm";
				continuouslegend "heatmapparm"/location=outside valign=bottom;
			endlayout;
		endgraph;
	end;
run;

proc sgrender data=ddw.test66 template=heatmap;
run;

data ddw.test67;
	input item:$50. index class:$20.;
	cards;
净资产收益率	0.18	行业标准
总资产收益率	0.1	行业标准
主营业务收益率	0.2	行业标准
资产负债比率	1	行业标准
流动比率	1	行业标准
速动比率	1	行业标准
总资产周转率	1.5	行业标准
主营业务收入增长率	0.68	行业标准
净资产增长率	0.27	行业标准
净资产收益率	0.15	A公司指标
总资产收益率	0.09	A公司指标
主营业务收益率	0.19	A公司指标
资产负债比率	0.48	A公司指标
流动比率	0.9	A公司指标
速动比率	0.9	A公司指标
总资产周转率	1.8	A公司指标
主营业务收入增长率	0.45	A公司指标
净资产增长率	0.23	A公司指标
;

proc gradar data=ddw.test67;
	chart item/ sumvar=index noframe starfill=solid cstarfill=orange;
	where class="A公司指标";        /*仅选择A公司的数据绘图*/
run;

proc gradar data=ddw.test67;
	chart item/ sumvar=index noframe overlayvar=class spiderweb cstar=(red,blue);
run;

data ddw.test68;
	do city="重庆","北京";
		do month="Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct",
			"Nov","Dec";           /*利用两个do循环输入数据*/
			input hfm@@;
			output;
		end;
	end;

	cards;
2.50	1.25	4.55	14.24 14.21 10.5	5.91	 1.24  2.06	3.86	5.01	4.52
0.78	0.76	6.14	24.71 53.77 67.73	47.26 19.37 15.28	9.88	9.11	3.96
;

proc gradar;
	chart month/sumvar=hfm calendar overlayvar=city nlevels=7 noframe;
run;

data ddw.test69;
	input prov$ id humidity@@;           /*变量中要包括id变量*/
	cards;
安徽	1	75	新疆	13	52	陕西	26	58
浙江	2	70	西藏	14	38	天津	28	56
江西	3	69	甘肃	15	52	云南	29	70
江苏	4	72	广西	16	77	广东	30	71
吉林	5	59	贵州	18	77	海南	31	79
青海	6	56	辽宁	19	64	四川	32	75
福建	7	69	内蒙古	20	50	重庆	33	82
黑龙江	8	60	宁夏	21	49	香港	34	.
河南	9	58	北京	22	52	澳门	35	.
河北	10	56	上海	23	70	台湾	36	.
湖南	11	73	山西	24	53			
湖北	12	71	山东	25	57			
;

proc gmap data=ddw.test69 map=maps.china;
	id id;            /*指定id变量为id，跟maps.china文件的id对应起来 */
	choro humidity;   /*指定反应变量为humidity，按humidity的值显示不同颜色*/
run;

%annomac;
%maplabel (maps.china,ddw.test69,labelout,prov,id,font=Arial,color=red,size=2,hsys=3);

proc gmap map=maps.china data=ddw.test69;
	id id;
	choro humidity/annotate=labelout;
run;

data ddw.test70;
	input prov$ id profit;
	cards;
安徽 1 178.4
浙江 2 279.2
江西 3 154.4
江苏 4 239.1
福建 7 166.5
上海 23 341.1
山东 25 213.5
;
run;

%annomac;
%maplabel (maps.china,ddw.test70,labelout,prov,id,font=Arial,color=red,size=3,hsys=3);

proc gmap map=maps.china data=ddw.test70;
	id id;
	choro profit/annotate=labelout;
	where id in (1,2,3,4,7,23,25);       /*利用where语句选择六省一市的id号*/
run;

legend1 label=("利润(万)");        /*将图例显示为“利润(万)”而不是变量名*/

proc gmap map=maps.china data=ddw.test70 density=2;
	/* density设置边界紧密程度为2，即较为分散*/
	id id;
	prism profit/legend=legend1 midpoints=150 250 350 range;
	where id in (1,2,3,4,7,23,25);       /*利用where语句选择六省一市的id号*/
run;

proc format;                   /*自定义我们想要的输出格式gfmt*/
	value gfmt 100-<200="100-199" 200-<300="200-299" 300-high=">=300";
run;

legend1 label=("利润(万)");      /*将图例显示为“利润(万)”而不是变量名*/

proc gmap map=maps.china data=ddw.test70 density=2;
	id id;
	prism profit/legend=legend1 discrete;

	/*这里一定要加上discrete选项，将profit作为离散值显示 */
	where id in (1,2,3,4,7,23,25);      /*利用where语句选择六省一市的id号*/
	format profit gfmt.;             /*将profit与定义的gfmt格式关联起来*/
run;
